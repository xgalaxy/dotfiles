#
# A simple theme that displays relevant, contextual information.
# Based on the theme 'sorin'.
#
# Authors:
#   Robert D. Blanchet Jr. <xgalaxy@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Load dependencies.
pmodload 'helper'

function build_left_prompt
{
  # Status symbols
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}✘"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}⚡"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{magenta}%}⚙"
  [[ -n "$symbols" ]] && echo -n "$symbols "

  # Context
  if [[ $UID -eq 0 || -n "$SSH_CLIENT" ]]; then
    echo -n "%(!.%{%F{yellow}%}.)%n@%m%f "
  fi

  # Directory
  local pwd="${PWD/#$HOME/~}"
  if [[ "$pwd" == (#m)[/~] ]]; then
    pwd="$MATCH"
    unset MATCH
  else
    pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi

  echo -n "%B%F{cyan}$pwd%f%b"
}

function vi-git-status()
{
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]=' %F{red}✗%f'
  fi
}

function prompt_xgalaxy_precmd
{
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Repository information.
  vcs_info

  #[ -n "$TMUX" ] && tmux setenv TMUXPWD_$(tmux display -p "#I_#P") "$PWD"
}

function prompt_xgalaxy_setup
{
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_xgalaxy_precmd

  # Shell editor states.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{green}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{cyan}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%B%F{yellow}♺%f%b'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❮%f%b'

  # Repository states.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' get-revision true
  zstyle ':vcs_info:*' use-simple true
  zstyle ':vcs_info:*' unstagedstr ' %F{red}✗%f'
  zstyle ':vcs_info:*' stagedstr ' %F{green}✔%f'
  zstyle ':vcs_info:*' actionformats ' %F{166}%b%f %F{default}@%f %F{blue}%.7i%f +%a%c%u'
  zstyle ':vcs_info:*' formats ' %F{166}%b%f %F{default}@%f %F{blue}%.7i%f%c%u'
  zstyle ':vcs_info:*' nvcsformats ''
  zstyle ':vcs_info:git*+set-message:*' hooks git-status

  # Define prompts.
  PROMPT='$(build_left_prompt) ${editor_info[keymap]} '
  RPROMPT='${vcs_info_msg_0_}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_xgalaxy_setup "$@"

